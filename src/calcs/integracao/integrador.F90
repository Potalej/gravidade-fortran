! ************************************************************
!! INTEGRADOR NUMERICO
!
! Objetivos:
!   Estrutura basica de um integrador numerico. As configuracoes
!   gerais de um metodo devem vir aqui, como tamanho de passo,
!   dimensao, massas, etc.
!
! Modificado:
!   08 de agosto de 2025
!
! Autoria:
!   oap
! 
MODULE integrador

	USE tipos
	USE funcoes_forca
  USE json_utils_mod
	IMPLICIT NONE
	PRIVATE
	PUBLIC integracao

!> Esta eh a classe de integracao, da qual todos os metodos de 
!  integracao devem ser filhos.
	TYPE, ABSTRACT :: integracao
		! m: Massas
		REAL(pf), ALLOCATABLE :: m(:)
		REAL(pf), ALLOCATABLE :: massasInvertidas(:,:)
		
		! Massas iguais
		LOGICAL  :: mi
		REAL(pf) :: m_esc, m_inv, m2
		REAL(pf128) :: m_esc_128, m_inv_128, m2_128

		! Distancias entre os corpos
		REAL(pf), ALLOCATABLE :: distancias(:)

		! h: Passo de integracao
		! G: Constante de gravitacao
		! potsoft: Softening do potencial
		REAL(pf) :: h, G, potsoft, potsoft2

		! dim: Dimensao do problema
		! N: Quantidade de partÃ­culas
		INTEGER :: dim = 3, N

		! Se vai ou nao usar paralelizacao
		LOGICAL :: paralelo = .FALSE., gpu = .FALSE.

		! Funcao de forcas (aceleracao)
		PROCEDURE(forcas_funcbase), POINTER, NOPASS    :: forcas_funcao    => NULL()
		PROCEDURE(forcas_mi_funcbase), POINTER, NOPASS :: forcas_mi_funcao => NULL()

	CONTAINS
		PROCEDURE :: iniciar, inicializar_massas, atualizar_constantes, &
								 forcas, metodo, metodo_mi, aplicar
								
	END TYPE integracao

CONTAINS

! ************************************************************
!! Construtor da classe
!
! Objetivos:
!   Define o principal, salvando os valores e inicializando o
!   metodo.
!
! Modificado:
!   08 de agosto de 2025
!
! Autoria:
!   oap
! 
SUBROUTINE iniciar (self, infos, timestep, massas, E0, J0)
  IMPLICIT NONE
  class(integracao), INTENT(INOUT) 			:: self
  TYPE(json_value), POINTER, INTENT(IN) :: infos
  REAL(pf), INTENT(IN), allocatable :: massas(:)
  REAL(pf), INTENT(IN) :: timestep, E0, J0(3)
  INTEGER :: a, i
	LOGICAL :: encontrado

	!> Quantidade de particulas
	self % N = SIZE(massas)

	!> Inicializando as variaveis ligadas as massas dos corpos
	CALL self % inicializar_massas(infos, massas)

	!> Constantes
	self % G = json_get_float(infos, 'G') ! Const. de Gravitacao
	self % h = timestep 									! Tamanho de passo
	!> Amortecedor
	self % potsoft = json_get_float(infos, 'integracao.amortecedor')
	self % potsoft2 = self % potsoft * self % potsoft
	
	!> Vetor de distancias dos corpos
	ALLOCATE(self % distancias(INT(self%N * (self%N-1)/2)))

	!> Uso (ou nao) do paralelismo
	CALL json % get(infos, 'paralelo', self % paralelo)
  CALL json % get(infos, 'gpu', self % gpu, encontrado)
  IF (.NOT. encontrado) self % gpu = .FALSE.

	!> Determinando as funcoes de forca a se utilizar
	!> Modulo: funcoes_forca
	CALL inicializar_forcas(self%mi, self%paralelo, self%gpu, &
                          self%forcas_funcao, self%forcas_mi_funcao)
END SUBROUTINE iniciar

! ************************************************************
!! Inicializa as massas
!
! Modificado:
!   08 de agosto de 2025
!
! Autoria:
!   oap
! 
SUBROUTINE inicializar_massas (self, infos, massas)
  CLASS(integracao), INTENT(INOUT) :: self
  TYPE(json_value), POINTER, INTENT(IN) :: infos
  REAL(pf), ALLOCATABLE :: massas(:)
  INTEGER :: a, i
  LOGICAL :: encontrado

  !> Massas iguais
	CALL json % get(infos, 'massas_iguais', self % mi, encontrado)
	IF (.NOT. encontrado) self % mi = .FALSE.

  !> Alocando vetor de massas
  ALLOCATE(self % m (self % N))
  self % m = massas

  !> Define com maior precisao se tiver massas iguais.
  !> Se nao tiver, define o vetor de massas invertidas
  IF (self % mi) THEN
    self % m_esc = massas(1)
    self % m_inv = 1.0_pf/self % m_esc
    self % m2 = self % m_esc * self % m_esc

    self % m_esc_128 = REAL(massas(1), KIND=pf128)
    self % m_inv_128 = 1.0_pf128 / self % m_esc_128
    self % m2_128 = self % m_esc_128 * self % m_esc_128
    
    ! Se 1/m - N < 1e-10, assume que m = 1/N
    ! Nesse caso, podemos melhorar a precisao
    IF (ABS(self % m_inv - self % N) < 1E-10) THEN
      self % m_esc = 1.0_pf / (self % N)
      self % m2 = 1.0_pf / (self % N * self % N)
      self % m_inv = self % N

      self % m_esc_128 = 1.0_pf128 / (self % N)
      self % m2_128 = 1.0_pf128 / (self % N * self % N)
      self % m_inv_128 = self % N
    ! Se m = 1
    ELSE IF (ABS(self % m_esc - 1) < 1E-10) THEN
      self % m_esc = 1.0_pf
      self % m2 = 1.0_pf
      self % m_inv = 1.0_pf

      self % m_esc_128 = 1.0_pf128
      self % m2_128 = 1.0_pf128
      self % m_inv_128 = 1.0_pf128
    ENDIF
  ELSE  
    ! vetor de massas invertidas
    ALLOCATE(self % massasInvertidas (self % N, self % dim))
    DO a = 1, self % N
      DO i = 1, self % dim
        self % massasInvertidas(a,i) = 1.0_pf/(massas(a))
      END DO
    END DO
  ENDIF
END SUBROUTINE inicializar_massas

! ************************************************************
!! Calculo das forcas conforme as massas
!
! Modificado:
!   08 de agosto de 2025
!
! Autoria:
!   oap
! 
FUNCTION forcas (self, R)
  IMPLICIT NONE
  class(integracao), INTENT(INOUT) :: self
  REAL(pf), DIMENSION(self % N, self % dim), INTENT(IN) :: R
  REAL(pf), DIMENSION(self % N, self % dim) :: forcas
  
  IF (self % mi) THEN
    forcas = self % forcas_mi_funcao(R, self%G, self%N, self%dim, &
                    self%potsoft2, self%distancias)
  ELSE
    forcas = self % forcas_funcao(self % m, R, self%G, self%N, self%dim, &
                    self%potsoft2, self%distancias)
  ENDIF
END FUNCTION forcas

! ************************************************************
!! Metodo numerico
!
! Objetivos:
!   Aplicacao do metodo em si.
!
! Modificado:
!   12 de julho de 2025
!
! Autoria:
!   oap
!
FUNCTION metodo (self, R, P, FSomas_ant)
  IMPLICIT NONE
  class(integracao), INTENT(INOUT) :: self
  REAL(pf), DIMENSION(self%N, self%dim), INTENT(IN) :: R, P, FSomas_ant
  REAL(pf), DIMENSION(3, self%N, self%dim) :: metodo
  
  ! Cada integrador precisa ter um metodo definido, que substitui esta funcao vazia
  WRITE (*,*) 'OPS'

END FUNCTION metodo

! ************************************************************
!! Metodo numerico (massas iguais)
!
! Objetivos:
!   Aplicacao do metodo em si.
!
! Modificado:
!   12 de julho de 2025
!
! Autoria:
!   oap
!
FUNCTION metodo_mi (self, R, P, FSomas_ant)
  IMPLICIT NONE
  class(integracao), INTENT(INOUT) :: self
  REAL(pf), DIMENSION(self%N, self%dim), INTENT(IN) :: R, P, FSomas_ant
  REAL(pf), DIMENSION(3, self%N, self%dim) :: metodo_mi
  
  ! Cada integrador precisa ter um metodo definido, que substitui esta funcao vazia
  WRITE (*,*) 'OPS'

END FUNCTION metodo_mi

! ************************************************************
!! Atualiza as constantes se necessario
!
! Modificado:
!   03 de junho de 2025
!
! Autoria:
!   oap
!
SUBROUTINE atualizar_constantes (self)
  IMPLICIT NONE
  class(integracao), INTENT(IN) :: self
END SUBROUTINE atualizar_constantes

! ************************************************************
!! Aplicacao do metodo
!
! Modificado:
!   08 de agosto de 2025
!
! Autoria:
!   oap
!
SUBROUTINE aplicar (self, R, P)
  class (integracao), INTENT(INOUT) :: self
  REAL(pf), DIMENSION(self%N, self%dim), INTENT(INOUT) :: R, P
  REAL(pf), DIMENSION(self%N, self%dim) :: FSomas_ant
  REAL(pf), DIMENSION(3, self%N, self%dim) :: resultado

  FSomas_ant = self%forcas(R)
  IF (self % mi) THEN
    resultado = self % metodo_mi(R, P, FSomas_ant)
  ELSE
    resultado = self % metodo(R, P, FSomas_ant)
  ENDIF

  R = resultado(1,:,:)
  P = resultado(2,:,:)
END SUBROUTINE

END MODULE integrador